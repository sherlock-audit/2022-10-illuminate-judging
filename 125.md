hansfriese

high

# Burn iPT token only for positive redemption

## Summary

Need to check the redeemed amount before burning iPT tokens.

## Vulnerability Detail

In the current implementation, for all kinds of `redeem` functions, the user's iPT tokens are burnt without checking the actually redeemed amount.
For example, let us say a user lent some underlying tokens and wants to redeem them after maturity.
Normally he needs to call a redemption function for the specific market first (so that `holdings` are updated) and then needs to call a `redeem(u,m)` function to get the underlying tokens back finally.
But if he forgot to call `redeem` for the specific market and nobody didn't call the specific redemption function yet, `holdings[m][u]` should be still zero.
So in this case the redeemed amount is zero.
But regardless of the redeemed amount, illuminate burns the whole user's iPT tokens.
This is very dangerous and user's funds can be lost by a simple mistake.

```solidity
function redeem(address u, uint256 m) external unpaused(u, m) {
    ...

    // Get the amount of tokens to be redeemed from the sender
    uint256 amount = token.balanceOf(msg.sender);

    // Calculate how many tokens the user should receive
    uint256 redeemed = (amount * holdings[u][m]) / token.totalSupply();

    // Update holdings of underlying
    holdings[u][m] = holdings[u][m] - redeemed;

    // Burn the user's principal tokens
    token.authBurn(msg.sender, amount);

    // Transfer the original underlying token back to the user
    Safe.transfer(IERC20(u), msg.sender, redeemed);

    ...
}
```

Below is a test using Foundry.

```solidity
// in the Lender.t.sol
function testAudit() public {
    uint testAmount = 1000000;
    uint256 matured = maturity - block.timestamp + 10;
    address u = Contracts.USDC;
    address A = address(100);
    deal(u, A, testAmount);

    vm.startPrank(A);
    IERC20(u).approve(address(l), 2**256 - 1);
    vm.stopPrank();

    // Set up the market
    deployMarket(Contracts.USDC);
    // illuminati principal token
    address iPT = mp.markets(Contracts.USDC, maturity, 0);

    emit log_string("START");

    emit log_named_uint("User A USDC", IERC20(u).balanceOf(A));
    emit log_named_uint("User A iPT", IERC20(iPT).balanceOf(A));
    emit log_named_uint("Total iPT", IERC20(iPT).totalSupply());
    emit log_named_uint("Lender (YIELD_TOKEN)", IERC20(Contracts.YIELD_TOKEN).balanceOf(address(l)));

    // Execute the lend
    emit log_string("");
    emit log_string("LEND");
    vm.startPrank(A);
    l.lend(
        uint8(2),
        Contracts.USDC,
        maturity,
        100000,
        Contracts.YIELD_POOL_USDC,
        0
    );
    vm.stopPrank();

    emit log_named_uint("Lender (YIELD_TOKEN)", IERC20(Contracts.YIELD_TOKEN).balanceOf(address(l)));
    emit log_named_uint("User A USDC", IERC20(u).balanceOf(A));
    emit log_named_uint("User A iPT", IERC20(iPT).balanceOf(A));
    emit log_named_uint("Total iPT", IERC20(iPT).totalSupply());

    emit log_string("");
    emit log_string("FORCE MATURE");
    skip(matured);

    // allow the lender to redeem
    vm.startPrank(address(l));
    IERC20(Contracts.YIELD_TOKEN).approve(address(r), ERC20(Contracts.YIELD_TOKEN).balanceOf(address(l)));
    vm.stopPrank();

    // redeem
    emit log_string("");
    emit log_string("REDEEM");
    emit log_string("A calls redeem(u,m) without calling redeem(2,u,m)");
    vm.startPrank(A);
    r.redeem(Contracts.USDC, maturity);
    vm.stopPrank();

    emit log_named_uint("Lender (YIELD_TOKEN)", IERC20(Contracts.YIELD_TOKEN).balanceOf(address(l)));
    emit log_named_uint("Reedemer (U)", IERC20(u).balanceOf(address(r)));
    emit log_named_uint("Holdings[u][m]", r.holdings(u, maturity));
    emit log_named_uint("User A USDC", IERC20(u).balanceOf(A));
    emit log_named_uint("User A iPT", IERC20(iPT).balanceOf(A));
    emit log_named_uint("Total iPT", IERC20(iPT).totalSupply());
}

```

Below is the result of the above test.

```solidity
  START
  User A USDC: 1000000
  User A iPT: 0
  Total iPT: 0
  Lender (YIELD_TOKEN): 0

  LEND
  Lender (YIELD_TOKEN): 100240
  User A USDC: 900000
  User A iPT: 100240
  Total iPT: 100240

  FORCE MATURE

  REDEEM
  A calls redeem(u,m) without calling redeem(2,u,m)
  Lender (YIELD_TOKEN): 100240
  Reedemer (U): 0
  Holdings[u][m]: 0
  User A USDC: 900000
  User A iPT: 0
  Total iPT: 0
```

## Impact

Users can lose iPT tokens without getting underlyings back by mistake.

## Code Snippet

https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Redeemer.sol#L428
https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Redeemer.sol#L528
https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Redeemer.sol#L470

## Tool used

Foundry

## Recommendation

Check if redeemed amount is positive before burning iPT token.
Below is an example.

```solidity
function redeem(address u, uint256 m) external unpaused(u, m) {
    ...

    // Calculate how many tokens the user should receive
    uint256 redeemed = (amount * holdings[u][m]) / token.totalSupply();

    // @audit revert if redeemed amount is zero
    require(redeemed > 0, "No underlyings redeemed");

    // Update holdings of underlying
    holdings[u][m] = holdings[u][m] - redeemed;

    // Burn the user's principal tokens
    token.authBurn(msg.sender, amount);

    // Transfer the original underlying token back to the user
    Safe.transfer(IERC20(u), msg.sender, redeemed);

    emit Redeem(0, u, m, redeemed, msg.sender);
}
```